@name TA Scan Generator
@model models/hunter/blocks/cube025x025x025.mdl
@trigger none
@persist PieceW PieceH PieceD PieceI PieceF PointID ExpEn OverZ
@persist [U R F P Org]:vector [A Ang]:angle X Y DX DY
@persist [ArrXU ArrXD ArrYL ArrYR ArrM]:array
@persist [Rail Chip]:entity RangerHit InsDistance
@persist [PieceType, PieceName, TypeSegment, PieceMode]:string

# These are for storing the minimums and maximums
# One array has these inside it: Arr = [Now, Min, Max]

function void getTrace(TrArr:array, TrFw:vector, TrUp:vector, TrPos:vector, TrLen, TrHolo)
{
    if(TrHolo > 0){ holoPos(TrHolo, TrPos), holoAng(TrHolo, (TrUp):toAngle(TrFw)) }
    TrRan = rangerOffset(TrLen,TrPos,TrFw)
    if(TrRan:hit())
    {
      if(TrArr[2,number] == 0){ TrArr[2,number] = TrLen}
      TrArr[1,number] = TrRan:distance()
      if(TrArr[1,number] < TrArr[2,number]){ TrArr[2,number] = TrArr[1,number] }
      if(TrArr[1,number] > TrArr[3,number]){ TrArr[3,number] = TrArr[1,number] }
      # print(_HUD_PRINTTALK,"MinMax: "+TrArr[1,number]+" <"+TrArr[2,number]+"/"+TrArr[3,number]+">")
    }
}

function void printPoint(PrType:string, PrName:string, PrMode:string, PrHash:string, PrPID:number, PrOrg:vector, PrAng:angle, PrZ)
{
    PrAng  = round(PrAng,2)
    PrOrg  = round(PrOrg,6)
    if(PrZ != 0){ PrOrg = PrOrg:setZ(PrZ) }
    StrAng = toString(PrAng):replace("[",""):replace("]","")
    StrOrg = toString(PrOrg):replace("[",""):replace("]","")
    StrIns = ""
    if(PrAng:pitch() == 0 && PrAng:yaw() == 0 && PrAng:roll() == 0){ StrAng = "" }
    if(PrOrg:x()     == 0 && PrOrg:y()   == 0 && PrOrg:z()    == 0){ StrOrg = "" }
    StrCls = entity():isConstrainedTo(1):type()
    if(StrCls == "prop_physics"){ StrCls = "" }

    StrPnt = ""

    if(PrMode == "DSV")
    {
        StrIns = "TRACKASSEMBLY_PIECES"+
            "\t"+PrHash+
            "\t"+"\""+PrType+"\""+
            "\t"+"\""+PrName+"\""+
            "\t"+toString(floor(abs(PrPID)))+
            "\t"+"\""+StrPnt+"\""+
            "\t"+"\""+StrOrg+"\""+
            "\t"+"\""+StrAng+"\""+
            "\t"+"\""+StrCls+"\""
    }
    elseif(PrMode == "LUA")
    {
        StrIns = "  asmlib.InsertRecord({"+
            "\""+PrHash+"\""+", "+
            "\""+PrType+"\""+", "+
            "\""+PrName+"\""+", "+
            toString(floor(abs(PrPID)))+", "+
            "\""+StrPnt+"\""+", "+
            "\""+StrOrg+"\""+", "+
            "\""+StrAng+"\""+", "+
            "\""+StrCls+"\""+"})"
    }
    print(_HUD_PRINTTALK,StrIns)
}

if(first() || dupefinished())
{
    # Chip based stuff
    runOnTick(1)
    
    # Get out E2
    Chip = entity()

    # Export mode. This can be /DSV/ or /LUA/
    PieceMode = "LUA"

    # What segment type are we processing
    # road     > Roads, like the PHX roads or Stephentechno
    # monorail > Monorail track beams like PHX monorail
    # general  > The two beam tracks segmentnts that we all love and joy
    TypeSegment = "general"

    # Piece type that you are using. It is mandatory
    # It is usually derived from the addon mame
    # that you are exporting
    PieceType = "#"

    # Piece name that you are using. This is actually
    # how the track piece will be called in the TA panel
    # Put a hastag to be autogenerated from the model on import
    PieceName = "#"

    # Point ID to be exported
    PointID = 1
    
    # Use this Override Z with acustom number if
    # Some of the tracks are compatible
    OverZ = 13.04688
    
    # This is automatically managed by the point ID selected
    # Set the coefficient before the sign() to adjust the process speed
    # The fighed the number the less iteration are made
    DX = 0.1
    DY = 0.1

    # Track piece width and height
    # The bigger the numbers the more cross section your track piece has
    # The width of the rail. It has to wrap the rail on width
    PieceW = 120
    # The height of the rail. It has to wrap the rail on height
    PieceH = 35
    # This tells the algorithm how deep should it search for rail surface
    PieceD = 24
    # This is an additional inset for the traces will use to hit the rail hull
    PieceI = 0.001
    # This shows how deep the first trace finds stuff usually less than PieceD
    PieceF = 0

    # Make sure that all the parameters are correct
    PieceW = abs(PieceW)
    PieceH = abs(PieceH)
    PieceD = abs(PieceD)
    PieceI = abs(PieceI)
    PieceF = abs(PieceF)
    DX     = abs(DX)
    DY     = abs(DY)

    # Holograms to display the stuff
    U = Chip:up()
    R = Chip:right()
    F = Chip:forward()
    P = Chip:pos()
    A = Chip:angles()
    Org = vec()
    Ang = ang()
    
    holoCreate(1,P, 0.3*vec(1,1,1))
    holoCreate(2,P, 0.3*vec(1,1,1))
    holoCreate(3,P, 0.3*vec(1,1,1))
    holoCreate(4,P, 0.3*vec(1,1,1))
    holoCreate(5,P, 0.3*vec(1,1,1))
    holoCreate(6,P, 0.3*vec(1,1,1))

    holoPos(1,P) holoColor(1,vec(0, 255, 0)) holoAng(1,F:toAngle( R)) holoModel(1,"sphere")
    holoPos(2,P) holoColor(2,vec(255,0,255)) holoAng(2,F:toAngle(-R)) holoModel(2,"cone")
    holoPos(3,P) holoColor(3,vec(255,0,255)) holoAng(3,F:toAngle(-U)) holoModel(3,"cone")
    holoPos(4,P) holoColor(4,vec(255,0,255)) holoAng(4,F:toAngle( U)) holoModel(4,"cone")
    holoPos(5,P) holoColor(5,vec(255,0,255)) holoAng(5,F:toAngle(-U)) holoModel(5,"cone")
    holoPos(6,P) holoColor(6,vec(255,0,255)) holoAng(6,F:toAngle( U)) holoModel(6,"cone")
    ArrXU = array(0,0,0)
    ArrXD = array(0,0,0)
    ArrYL = array(0,0,0)
    ArrYR = array(0,0,0)
    ArrM  = array(0,0,0)
    X = -PieceW
    Y = -PieceH
    ExpEn = 1
    
    # If this entity is not traced the process will not start
    # This is our track oiece that we are generating the point for
    Rng = rangerOffset(PieceD,P,-F)

    # Make sure we hit someting otherwise it has no point of doing this
    RngHit = Rng:hit()
    if(RngHit)
    {
      Rail = Rng:entity()
      PieceF = Rng:distance()
      print(_HUD_PRINTTALK,"Trace distance: " + PieceF)
    }else{
      Rail = noentity()
      PieceF = 0
      print(_HUD_PRINTTALK,"Tracing failed !")
    }
}

if(PieceF != 0 && ExpEn != 0)
{
  Chip:setColor(0,255,0,255)
  if(X < PieceW)
  {
    getTrace(ArrXU,-U, F, P - X * R + PieceH * U - PieceD * F, 2 * PieceH, 2) # Up
    getTrace(ArrXD, U, F, P - X * R - PieceH * U - PieceD * F, 2 * PieceH, 3) # Down
    getTrace(ArrM ,-F, U, P - X * R                          ,     PieceF, 4) # Middle
    X = X + DX
  }
  if(Y < PieceH)
  {
    getTrace(ArrYL, -R, U, P - Y * U + PieceW * R - PieceD * F, 2 * PieceW, 5) # Left
    getTrace(ArrYR,  R, U, P - Y * U - PieceW * R - PieceD * F, 2 * PieceW, 6) # Right
    Y = Y + DY
  }
  if(X >= PieceW && Y >= PieceH)
  {
    if(TypeSegment == "general")
    {
      MZ = PieceD - PieceF
      MX = ArrYL[2,number] + (2 * PieceW - ArrYL[2,number] - ArrYR[2,number]) / 2
      MY = ArrXU[2,number] + (ArrXU[3,number] - ArrXU[2,number]) / 2
      Pos = P + PieceW * R + PieceH * U - PieceD * F
      Pos = Pos + MZ * F
      Pos = Pos - MX * R
      Pos = Pos - MY * U
      Org = Rail:toLocalAxis(Pos - Rail:pos())
      Ang = Rail:toLocal((F):toAngle(U))
    }
    holoPos(1,Org:rotate(Rail:angles()) + Rail:pos())
    printPoint(PieceType, PieceName, PieceMode, Rail:model(), PointID, Org, Ang, OverZ)
    Chip:setColor(255,255,255,255)
    ExpEn = 0
  }
}
else
{
  if(ExpEn != 0){ Chip:setColor(255,0,0,255) }else{Chip:setColor(255,255,255,255) }
}






